apply plugin: 'com.android.application'

static def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy-MM-dd-HH-mm')
    return formattedDate
}

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.example.xiaolanyun.ldmart"
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        /*
        启用multiDex支持 解决方法数 超过 64k 的问题
         */
        multiDexEnabled true
        /**
         * 支持背景模糊
         */
        renderscriptTargetApi 25
        renderscriptSupportModeEnabled true
    }
    buildTypes {
        release {
            /***
             * 代码混淆
             */
            minifyEnabled false
            /*加载默认混淆配置文件*/
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            /***
             * 自定义字段【true：测试地址 false：线上地址】
             */
            buildConfigField "boolean", "IS_TEST", "false"
            /***
             * 自定义字段【true: Jenkins 打包 false: AS 打包】
             */
            buildConfigField "boolean", "IS_JENKINS", "${IS_JENKINS}"
            /***
             * zipalign优化
             */
            zipAlignEnabled true
            /**
             * 移除无用的resource文件
             */
            shrinkResources false


        }
        debug {

            /***
             * 自定义字段【true：测试地址 false：线上地址】
             */
            buildConfigField "boolean", "IS_TEST", "true"
            /***
             * 自定义字段【true: Jenkins 打包 false: AS 打包】
             */
            buildConfigField "boolean", "IS_JENKINS", "${IS_JENKINS}"

            minifyEnabled false


            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    /*修改生成的apk名字*/
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def newName
            def timeNow = getDate().substring(5) // 去掉年


            if ("true" == IS_JENKINS) {

                if (variant.buildType.name == 'debug') {
                    newName = "tutuenglish-v${APP_VERSION}-debug.apk"
                } else {
                    newName = "tutuenglish-v${APP_VERSION}-release.apk"
                }

            } else {

                newName = "tutuenglish-v${APP_VERSION}-${timeNow}-" + variant.buildType.name + ".apk"

            }

            // jenkins打包时修改输出路径保存所有打包记录

            if ("true" == IS_JENKINS) {
                // 修改输出路径
                if ('debug' == variant.buildType.name) {
                    variant.getPackageApplication().outputDirectory = new File(project.rootDir.absolutePath + "/apk/debug/")
                } else {
                    variant.getPackageApplication().outputDirectory = new File(project.rootDir.absolutePath + "/apk/release/")
                }
            }

            outputFileName = newName

        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.greenrobot:greendao-gradle-plugin:3.2.1'
    }
}

apply plugin: 'org.greenrobot.greendao'

greendao {
    schemaVersion 1
    daoPackage 'com.example.leidong.ldmart.greendao'
    targetGenDir 'src/main/java'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //noinspection GradleCompatible
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    /*ButterKnife*/
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    /*BottomBar*/
    implementation 'com.roughike:bottom-bar:2.3.1'

    /*RecyclerView*/
    implementation 'com.android.support:recyclerview-v7:26.1.0'

    /*Gson*/
    implementation 'com.google.code.gson:gson:2.8.2'

    /*GreenDao*/
    implementation 'org.greenrobot:greendao:3.0.1'
    implementation 'org.greenrobot:greendao-generator:3.0.0'

    /*Picasso*/
    implementation 'com.squareup.picasso:picasso:2.71828'
}
